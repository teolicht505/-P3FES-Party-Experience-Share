set levelUpAllMembers 0x009D5000
set setCharacterFlagsTheAnswer 0x009D5100

ASM_START 0x001F1828
    daddiu $s0, $zero, 0x2 // First character id (Yukari)
    j 0x009D5000
    daddu $s1, $zero, 0xB // Characters' id end
ASM_END

ASM_START levelUpAllMembers
    j 0x009D5100 // Set character flags in the Answer
    nop
    doWhileStart: addiu $v0, $zero, 0x6
    beq $v0, $s0, checkCurrentCharacter
    addiu $a0, $zero, 0x14a // Flag of character 0x6

    addiu $v0, $zero, 0x6
    slt $v0, $s0, $v0
    bne $v0, $zero, checkCurrentCharacter
    addiu $a0, $s0, 0x140 // Flag of characters 0x2-0x5
    addiu $a0, $s0, 0x13f // Flag of characters 0x7-0x10

    checkCurrentCharacter: jal 0x0016f190 // Check current character bit
    nop
    beq $v0, $zero, doWhileNextIteration // Skip to next iteration if character is not available
    nop
    move $a0, $s0
    jal 0x0016C470
    nop 
    andi $a0,$v0,0x00FF // Current character level
    lw $a1,0x2A54($s2)
    lw $a2,0xB8($s2)
    lw $a3,0x2A58($s2)
    lw $t0,0x11C($s2) // Number of members in battle
    jal 0x001FBDF0 // Experience calculator
    nop 
    move $a0, $s0 // Current character
    daddu $a1,$v0,$zero // Current experience to add
    jal 0x001F9E90
    nop 
    doWhileNextIteration: addiu $s0,$s0,0x1
    doWhileEnd: bne $s0, $s1, doWhileStart
    nop

    j 0x001f188c
    nop
ASM_END

// In the answer, Fuuka sets the character flags when asked for the current everyone status
// We will activate the same flags she does with flowscript only for the the Answer with assembly
ASM_START setCharacterFlagsTheAnswer
    lw $a0,-0x4D4C($gp) // Load flag that tells if we are in The Answer
    beq $a0,$zero,endFlagSetting // If we are not in the answer skip the flag setting
    nop 
    jal 0x00395170
    addiu $a0,$zero,0x142
    jal 0x00395170
    addiu $a0,$zero,0x146
    jal 0x00395170
    addiu $a0,$zero,0x147
    jal 0x00395170
    addiu $a0,$zero,0x148
    jal 0x00395170
    addiu $a0,$zero,0x14A
    jal 0x003951d0
    addiu $a0,$zero,0x2DE
    beq $v0,$zero,endFlagSetting
    nop 
    jal 0x00395170
    addiu $a0,$zero,0x145
    jal 0x00395170
    addiu $a0,$zero,0x149
    jal 0x003951d0
    addiu $a0,$zero,0x2D2
    beq $v0,$zero,endFlagSetting
    nop 
    jal 0x00395170
    addiu $a0,$zero,0x144
    endFlagSetting: j 0x009D5008
    nop
ASM_END